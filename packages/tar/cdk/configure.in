dnl
dnl $Id$
dnl
AC_INIT()
AM_INIT_AUTOMAKE(cdk, `cat VERSION`)

dnl deal with options
SGMLDIR=""
AC_ARG_WITH(sgmldir,
      [  --with-sgmldir=DIR  where to find the SGML dir ],
      [  if test -n "$withval"
         then
          SGMLDIR="$withval"
         fi
      ]
)
AC_SUBST(SGMLDIR)
AC_MSG_RESULT(Using sgml dir $SGMLDIR)

AC_ARG_ENABLE(freecompiler,
[  --enable-freecompiler    Use jikes of gcj if available],
[case "${enableval}" in
yes) freecompiler=true ;;
no)  freecompiler=false ;;
*) AC_MSG_ERROR(bad value ${diableval} for --enable-freecompiler) ;;
esac],[freecompiler=false])
AM_CONDITIONAL(FREECOMPILER, test x$freecompiler = xtrue)

dnl Checks for programs.
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

PACKAGE=cdk
AC_SUBST(PACKAGE)
CDKJARDIR=$prefix/share/java
AC_SUBST(CDKJARDIR)
CDKLIBDIR=$prefix/lib/cdk
AC_SUBST(CDKLIBDIR)

AC_CHECK_PROG(HASJIKES, jikes, yes, no)
AC_PATH_PROG(PATHTOJIKES, jikes)
AM_CONDITIONAL(WITHJIKES, test "x$HASJIKES" = "xyes")
AC_CHECK_PROG(HASGCJ, gcj, yes, no)
AC_PATH_PROG(PATHTOGCJ, gcj)
AM_CONDITIONAL(WITHGCJ, test "x$HASGCJ" = "xyes")

if test -x "$JAVA_HOME/bin/java"
then
    AC_MSG_CHECKING(for java)
    AC_MSG_RESULT($JAVA_HOME/bin/java)
    PATHTOJAVA=$JAVA_HOME/bin/java
    AC_SUBST(PATHTOJAVA)
else
    AC_CHECK_PROG(HASJAVA, java, yes, no)
    if test "xyes" = "x$HASJAVA"
    then
        AC_PATH_PROG(PATHTOJAVA, java, no)
    fi
fi

AC_MSG_CHECKING(for rt.jar lib)
for d in "/usr/lib/j2re1.3/lib"
do
    if test -f "$d/rt.jar"
    then
        PATHTORTJAR="$d/rt.jar"
    fi
done
if test "x$PATHTORTJAR" = "x"
then
    AC_MSG_RESULT(no)
else
    AC_MSG_RESULT($PATHTORTJAR)
fi
AC_SUBST(PATHTORTJAR)

AC_MSG_CHECKING(for Java3D lib)
AC_ARG_WITH(java3ddir,
      [  --with-java3ddir=DIR    path to the Java 3D installation directory
        ],
      [  if test -n "$withval"
         then
          JAVA3DDIR="$withval"
         fi
        ])
if test -f "$JAVA3DDIR/lib/ext/j3dcore.jar"
then
    PATHTO3DJAR="$JAVA3DDIR/lib/ext/j3dcore.jar:$JAVA3DDIR/lib/ext/j3dutils.jar:$JAVA3DDIR/lib/ext/vecmath.jar"
else
    if test "x$prefix" = xNONE
    then
        # order these with prefered one at the end
        try_dirs="$ac_default_prefix"
    else
        try_dirs="$prefix"
    fi

    # Debian GNU/Linux Blackdown path: /usr/lib/j2re1.3
    try_dirs="$try_dirs /usr/lib/j2re1.3 $JAVA_HOME $JAVA_HOME/jre"

    for d in $try_dirs
    do
        if test -r "$d/lib/ext/j3dcore.jar"
        then
            PATHTO3DJAR="$d/lib/ext/j3dcore.jar:$d/lib/ext/j3dutils.jar:$d/lib/ext/vecmath.jar"
            JAVA3DDIR="$d/lib/ext"
        fi
    done
fi
if test "x$PATHTO3DJAR" = "x"
then
    AC_MSG_RESULT(no)
else
    AC_MSG_RESULT($PATHTO3DJAR)
fi
AC_SUBST(JAVA3DDIR)
AC_SUBST(PATHTO3DJAR)

dnl needed for check_xml
AC_CHECK_PROG(HASXSLTPROC, xsltproc, yes, no)
AC_PATH_PROG(PATHTOXSLTPROC, xsltproc)
AC_CHECK_PROG(HASXMLLINT, xmllint, yes, no)
AC_PATH_PROG(PATHTOXMLLINT, xmllint)

dnl other
AC_CHECK_PROG(HASANT, ant, yes, no)
AC_PATH_PROG(PATHTOANT, ant)
AC_CHECK_PROG(HASTAR, tar, yes, no)
AC_PATH_PROG(PATHTOTAR, tar, no)
AC_CHECK_PROG(HASGZIP, gzip, yes, no)
AC_PATH_PROG(PATHTOGZIP, gzip, no)

dnl detection copied from Lire 1.0 (www.logreport.org)
dnl Try to find the SGML directory layout
dnl Find a list of potential SGML/XML trees, we will look into subdirectories
dnl of this one to find the DSSSL/DTD/XSLT stuff.
sgmldirs=$SGMLDIR
try_dirs="/usr/lib/sgml /usr/share/sgml /usr/local/lib/sgml /usr/local/share/sgml /usr/share/xml /usr/local/share/xml /usr/local/share/xsl"
if test x"$prefix" != "xNONE"
then
    try_dirs="$try_dirs $prefix/lib/sgml $prefix/share/sgml $prefix/share/xml"
fi
AC_MSG_CHECKING(for SGML/XML trees)
for d in $try_dirs
do
    if test -d $d
    then
	if echo "$sgmldirs" | grep $d > /dev/null
	then
	    :
	else
	    sgmldirs="$sgmldirs $d"
	fi
    fi
done
if test -z "$sgmldirs"
then
    sgmldirs="none"
fi
AC_MSG_RESULT($sgmldirs)
AC_MSG_CHECKING(for DocBook XSL stylesheets)
if test -z "$DBK_XSL_STYLESHEETS" && test -z "$PATHTODBKXSLHTML"
then
    for d in $sgmldirs
    do
	for f in stylesheet/xsl/docbook stylesheet/xsl stylesheet docbook/stylesheet/xsl docbook/xsl docbook
	do
	    dir=
            if test -f "$d/$f/xhtml/docbook.xsl"
	    then
		dir="$d/$f"
	    elif test -f "$d/$f/modular/xhtml/docbook.xsl"
	    then
		dir="$d/$f/modular"
	    elif test -f "$d/$f/nwalsh/xhtml/docbook.xsl"
	    then
		dir="$d/$f/nwalsh"
	    fi
	    if test -n "$dir"
	    then
		AC_MSG_RESULT("$dir")
		PATHTODBKXSLHTML="$dir/html"
		PATHTODBKXSLCOMMON="$dir/common/common.xsl"
	    fi
	    test -n "$PATHTODBKXSLHTML" && break
	done
	test -n "$PATHTODBKXSLHTML" && break
    done
else
    if test -n "$DBK_XSL_STYLESHEETS"
    then
	AC_MSG_RESULT($DBK_XSL_STYLESHEETS)
	if ! test -d "$DBK_XSL_STYLESHEETS"
	then
	    AC_MSG_WARN($DBK_XSL_STYLESHEETS isn't present on the system)
	fi
	PATHTODBKXSLHTML="$DBK_XSL_STYLESHEETS/html"
	PATHTODBKXSLCOMMON="$DBK_XSL_STYLESHEETS/common/common.xsl"
    else
	AC_MSG_RESULT($PATHTODBKXSLHTML)
	AC_MSG_RESULT($PATHTODBKXSLCOMMON)

	if ! test -f "$PATHTODBKXSLHTML"
	then
	    AC_MSG_WARN($PATHTODBKXSLHTML isn't present on system)
	fi
    fi
fi
if test "x$PATHTODBKXSLHTML" = "x"
then
    AC_MSG_RESULT(no)
fi
AC_SUBST(PATHTODBKXSLHTML)
AC_SUBST(PATHTODBKXSLCOMMON)


AC_OUTPUT([
   Makefile
   jar/Makefile
   jar/libio/Makefile
   doc/Makefile
   bin/Makefile
   bin/cdk-dbadmin
   bin/cdk-download
   bin/cdk-fileconvert
   bin/cdk-fingerprinter
   bin/cdk-guessformat
   bin/cdk-iupacname
   bin/cdk-substructure
   bin/cdk-test
   bin/cdk-validate
   bin/cdk-view
   man/Makefile
   man/xsl/Makefile
   man/xsl/db2man.xsl
])

